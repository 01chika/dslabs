// Gradle 6.4.1

plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

sourceCompatibility = '14'
targetCompatibility = '14'

sourceSets {
  main {
    java {
      srcDirs = ['framework/src']
    }
  }

  grader {
    java {
      srcDirs = ['framework/tst']
    }
    compileClasspath += main.output
    annotationProcessorPath += main.annotationProcessorPath
  }

  test {
    java {
      srcDirs = ['framework/tst-self']
    }
    compileClasspath += grader.compileClasspath + grader.output
    runtimeClasspath += grader.runtimeClasspath
    annotationProcessorPath += grader.annotationProcessorPath
  }

  labs {
    java {
      srcDirs = [ 'labs/lab0-pingpong/src',
                  'labs/lab1-clientserver/src',
                  'labs/lab2-primarybackup/src',
                  'labs/lab3-paxos/src',
                  'labs/lab4-shardedstore/src' ]
    }
    compileClasspath += main.compileClasspath + main.output
    runtimeClasspath += main.runtimeClasspath
    annotationProcessorPath += main.annotationProcessorPath
  }

  labsTests {
    java {
      srcDirs = [ 'labs/lab0-pingpong/tst',
                  'labs/lab1-clientserver/tst',
                  'labs/lab2-primarybackup/tst',
                  'labs/lab3-paxos/tst',
                  'labs/lab4-shardedstore/tst']
    }
    compileClasspath += grader.compileClasspath + grader.output + labs.output
    runtimeClasspath += grader.runtimeClasspath + labs.output
    annotationProcessorPath += grader.annotationProcessorPath
  }
}

configurations {
  sources
  graderSources.extendsFrom sources

  graderImplementation.extendsFrom implementation
}

repositories {
  mavenCentral()
}

dependencies {
  implementation      'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'
  sources             'org.projectlombok:lombok:1.18.12:sources'

  implementation 'com.google.guava:guava:29.0-jre'
  sources        'com.google.guava:guava:29.0-jre:sources'
  implementation 'org.apache.commons:commons-lang3:3.10'
  sources        'org.apache.commons:commons-lang3:3.10:sources'

  implementation 'com.fasterxml.jackson.core:jackson-core:2.11.0'
  sources        'com.fasterxml.jackson.core:jackson-core:2.11.0:sources'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
  sources        'com.fasterxml.jackson.core:jackson-databind:2.11.0:sources'

  graderImplementation 'junit:junit:4.13'
  graderSources        'junit:junit:4.13:sources'
  graderImplementation 'io.github.kostaskougios:cloning:1.10.3'
  graderSources        'io.github.kostaskougios:cloning:1.10.3:sources'

  graderImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.11.0'
  graderSources        'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.11.0:sources'
  graderImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.11.0'
  graderSources        'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.11.0:sources'
  graderImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0'
  graderSources        'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0:sources'
}

// Create reproducible jars
tasks.withType(AbstractArchiveTask) {
  preserveFileTimestamps = false
  reproducibleFileOrder = true
}

task scaffoldingJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  classifier = 'compile'
  from sourceSets.main.output
  configurations = [ project.configurations.runtimeClasspath,
                     project.configurations.sources ]
}

task graderJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  from sourceSets.main.output,
       sourceSets.grader.output
  configurations = [ project.configurations.graderRuntimeClasspath,
                     project.configurations.graderSources ]
}

task sourcesJar(type: ShadowJar) {
  archiveBaseName = 'framework'
  classifier = 'sources'
  from sourceSets.main.java,
       sourceSets.grader.java,
  configurations = []
}

jar.enabled = false

javadoc {
  title = 'Distributed Systems Labs Framework'
  destinationDir = file("$buildDir/doc")
  options.addStringOption('notimestamp', '-quiet')
}

tasks.assemble.dependsOn tasks.scaffoldingJar
tasks.assemble.dependsOn tasks.graderJar
tasks.assemble.dependsOn tasks.sourcesJar
tasks.assemble.dependsOn tasks.javadoc

// copy dependencies into directories that IntelliJ can find
task copyDependencies {
  doLast {
    copy {
      from configurations.compileClasspath,
           configurations.runtimeClasspath,
           configurations.sources
      into "deps/compile"
    }
    copy {
      from configurations.graderCompileClasspath,
           configurations.graderRuntimeClasspath,
           configurations.graderSources
      into "deps/test"
    }
  }
}

// Put gradle wrapper in a hidden folder
wrapper {
  jarFile = file('.gradle-wrapper/gradle-wrapper.jar')
}

// Run tests with illegal-access=deny to ensure that these warnings are caught
test.jvmArgs(['-ea',
              '--illegal-access=deny',
              '--add-opens', 'java.base/jdk.internal.reflect=ALL-UNNAMED',
              '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
              '--add-opens', 'java.base/java.util=ALL-UNNAMED',
              '--add-opens','java.base/java.util.concurrent.atomic=ALL-UNNAMED'])
